import org.apache.tools.ant.filters.*

buildscript {
	repositories { jcenter() }
	dependencies {
		classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.2'
		classpath 'com.bmuschko:gradle-izpack-plugin:2.1'
	}
}

repositories { mavenCentral() }

apply plugin: 'java'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'eclipse'
apply plugin: 'application'
apply plugin: 'com.bmuschko.izpack'

sourceCompatibility = 1.7
version = '1.1.0'

def applicationName = 'OneClipboard'
def jarName = applicationName + '.jar'

dependencies {
	compile 'com.outr.javasysmon:javasysmon_2.10:0.3.4'
	compile 'org.bouncycastle:bcprov-jdk15on:1.46'
	compile project(':oneclipboardlib')
	izpack 'org.codehaus.izpack:izpack-standalone-compiler:4.3.5'
}

sourceSets.main.java.srcDirs = ['src/main/java']
sourceSets.main.resources.srcDirs = ['src/main/resources']

mainClassName = "com.cb.oneclipboard.desktop.Client"

processResources {
	filesMatching('**/config.properties') {
		filter ReplaceTokens, tokens: [
				"serverHostName": project.serverHostName,
				"serverPublicKeyStorePass": project.serverPublicKeyStorePass,
				"clientPrivateKeyStorePass": project.clientPrivateKeyStorePass,
				"version": version
		]
	}
	doLast {
		copy{
			from([project.serverPublicKeyStorePath, project.clientPrivateKeyStorePath])
			into "${destinationDir}"
		}
	}
}

shadowJar {
	archiveName = jarName
}

izpack {
	izPackCreateInstaller.dependsOn shadowJar
	baseDir = file("$buildDir")
	appProperties = [
			'app.name': applicationName,
			'app.jar': 'OneClipboard.jar',
			'app.version': version,
			'app.jar.path': 'libs/' + jarName,
			'app.author.name': project.authorName,
			'app.author.email': project.authorEmail,
			'app.website': project.appWebsite
	]
}
