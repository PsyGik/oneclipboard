apply plugin: 'com.android.application'

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.3.1'
    }
}

repositories {
    jcenter()
    flatDir{
        dirs 'libs'
    }
}

android {
    compileSdkVersion 23
    buildToolsVersion '23.0.1'
    useLibrary  'org.apache.http.legacy'

     sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }

        instrumentTest.setRoot('tests')
    }

    defaultConfig {
        applicationId "com.cb.oneclipboard"
        minSdkVersion 16
        targetSdkVersion 23
        versionCode 1
        versionName "0.0.5"
    }

    lintOptions {
      abortOnError false
  }
}

dependencies {
    compile project(':oneclipboardlib')
    compile fileTree(dir: 'libs', include: ['*.jar'])
}

android.applicationVariants.all { variant ->
	def serverHostName = project.hasProperty("serverhost") ? project.property("serverhost") : "localhost"
	variant.mergeAssets.doLast {
		copy {
			from("${projectDir}/assets") { include "*.properties" }
			into("${buildDir}/assets/${variant.dirName}")
			filter {
				it.replace('localhost', serverHostName)
			}
		}
	}
}

apply plugin: 'eclipse'

eclipse.ext {
	genDir = new File("${projectDir}/gen")
}

// http://blog.gouline.net/2013/11/02/new-build-system-for-android-with-eclipse/

eclipse.classpath.plusConfigurations += configurations.compile

eclipse.classpath.file {
    beforeMerged { classpath ->
        classpath.entries.removeAll() { c ->
            c.kind == 'src'
        }
    }

    withXml {
		eclipse.genDir.mkdir()

        def node = it.asNode()

        node.appendNode('classpathentry kind="src" path="src"')
        node.appendNode('classpathentry kind="src" path="gen"')

        node.children().removeAll() { c ->
            def path = c.attribute('path')
            path != null && (
                    path.contains('/com.android.support/support-v4')
                    )
        }
    }
}

eclipse.project {
    name = 'oneclipboardandroid'

    natures 'com.android.ide.eclipse.adt.AndroidNature'
    buildCommand 'com.android.ide.eclipse.adt.ResourceManagerBuilder'
    buildCommand 'com.android.ide.eclipse.adt.PreCompilerBuilder'
    buildCommand 'com.android.ide.eclipse.adt.ApkBuilder'
}
